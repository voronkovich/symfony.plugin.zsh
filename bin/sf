#!/usr/bin/env zsh

declare -r app_name="${0##*/}"

main() {
    config_load_env_files

    declare -r app_runner="$(runner_detect)"
    declare -r app_console="$(console_detect)"

    [[ -n "${app_runner}" ]] || fail 'Runner is not configured.'

    local cmd="${1}" && shift 2>/dev/null

    case "${cmd}" in
        serve|start)
            app_serve "$@"
            ;;
        stop)
            app_stop "$@"
            ;;
        status)
            app_status "$@"
            ;;
        open)
            app_open "$@"
            ;;
        psql)
            app_psql "$@"
            ;;
        mails)
            app_mails "$@"
            ;;
        run)
            run "$@"
            ;;
        php)
            php_run "$@"
            ;;
        composer)
            composer_run "$@"
            ;;
        phpunit)
            phpunit_run "$@"
            ;;
        phive)
            phive_run "$@"
            ;;
        *)
            console_run "${cmd}" "$@"
    esac
}

app_serve() {
    case "$(runner_name)" in
        docker|docker-compose)
            exec "$(docker_compose_command)" up "$@"
            ;;
        ddev)
            exec ddev start "$@"
            ;;
        symfony)
            exec symfony serve "$@"
            ;;
        local)
            exec local_serve "$@"
            ;;
        *)
            runner_not_supported
            ;;
    esac
}

app_stop() {
    case "$(runner_name)" in
        docker|docker-compose)
            exec "$(docker_compose_command)" down "$@"
            ;;
        ddev)
            exec ddev stop "$@"
            ;;
        symfony)
            exec symfony server:stop "$@"
            ;;
        *)
            runner_not_supported
            ;;
    esac
}

app_status() {
    echo "Runner: ${app_runner}"
    echo "Console: ${app_console}\n"

    php_run --version

    echo

    case "$(runner_name)" in
        docker|docker-compose)
            exec "$(docker_compose_command)" ps "$@"
            ;;
        ddev)
            exec ddev status "$@"
            ;;
        symfony)
            echo "Symfony binary: $(which symfony)\n"
            exec symfony server:status "$@"
            ;;
        local)
            echo "PHP binary: $(which php)"
            ;;
    esac
}

app_open() {
    case "$(runner_name)" in
        docker|docker-compose)
            open 'https://localhost' 2>/dev/null
            ;;
        ddev)
            ddev launch "$@" 2>/dev/null
            ;;
        symfony)
            symfony open:local "$@"
            ;;
        *)
            open 'http://127.0.0.1:8000' 2>/dev/null
            ;;
    esac
}

app_psql() {
    case "$(runner_name)" in
        ddev)
            ddev psql "$@"
            ;;
        symfony)
            symfony run psql "$@"
            ;;
        *)
            runner_not_supported
            ;;
    esac
}

app_mails() {
    case "$(runner_name)" in
        ddev)
            ddev launch -m "$@" 2>/dev/null
            ;;
        symfony)
            symfony open:local:webmail "$@"
            ;;
        *)
            runner_not_supported
            ;;
    esac
}

run() {
    runner_is 'symfony' \
        && symfony run "$@" \
        || runner_run "$@"
}

php_run() {
    runner_run php "$@"
}

composer_run() {
    runner_is 'symfony' \
        && symfony composer "$@" \
        || runner_run composer "$@"
}

phpunit_run() {
    declare -r phpunit="$(phpunit_find)"

    [[ -n "${phpunit}" ]] \
        && php_run "${phpunit}" "$@" \
        || run phpunit "$@"
}

phpunit_find() {
    find_first_existing_file \
        'bin/phpunit' \
        'vendor/bin/phpunit' \
    ;
}

phive_run() {
    declare -r phive="$(phive_find)"

    [[ -n "${phive}" ]] \
        && php_run "${phive}" "$@" \
        || run phive "$@"
}

phive_find() {
    declare phive="$(find_first_existing_file \
        'bin/phive' \
        'bin/phive.phar' \
        'phive' \
        'phive.phar' \
    )"

    if [[ -n "${phive}" ]]; then
        echo "${phive}"
        return
    fi

    if runner_is 'symfony'; then
        phive="$(which phive 2>/dev/null)"

        if [[ $? -eq 0 && -f "${phive}" ]]; then
            echo "${phive}"
            return
        fi
    fi

    return 1
}

console_run() {
    if [[ ! -f "${app_console}" ]]; then
        error "Console not found: ${app_console}"
        return 1
    fi

    if runner_is 'symfony' && [[ "${app_console}" == 'bin/console' ]]; then
        symfony console "$@"
        return $?
    fi

    runner_run php "${app_console}" "$@"
}

console_detect() {
    echo "${SF_CONSOLE:-bin/console}"
}

symfony_cli_enabled() {
    feature_enabled "${SF_SYMFONY_CLI:-on}"
}

ddev_enabled() {
    feature_enabled "${SF_DDEV:-on}"
}

docker_enabled() {
    feature_enabled "${SF_DOCKER:-on}"
}

docker_find_compose_file() {
    find_first_existing_file \
        "${PWD}/docker-compose.yml" \
        "${PWD}/docker-compose.yaml" \
    ;
}

docker_compose_command() {
    (( $+commands[docker-compose] )) \
        && echo 'docker-compose' \
        || echo 'docker compose'
}

local_serve() {
    local help port=('--port' '8000') root=('--document-root' 'public')

    zmodload zsh/zutil
    zparseopts -D -F -K -- \
        {h,-help}=help \
        -no-ansi=noansi \
        -port:=port \
        -document-root:=root \
        || return 1

    if [[ -n "${help}" ]]; then
        cat <<HELP
Run a local web server

Usage:

  ${app_name} [options]

Options:

  --port=value           Preferred HTTP port [default: ${port[2]}]
  --document-root=value  Project document root [default: ${root[2]}]
  -h, --help             Show this help
HELP
        return 0
    fi

    exec php -S "127.0.0.1:${port[2]/=}" -t "${root[2]/=}"
}

local_enabled() {
    feature_enabled "${SF_LOCAL:-on}"
}

runner_run() {
    runner_is 'local' && "$@" || "${(@s: :)app_runner}" "$@"
}

runner_detect() {
    declare runner="${SF_RUNNER}"

    if [[ -n "${runner}" ]]; then
        echo "${runner}"
        return
    fi

    if ddev_enabled && [[ -d "${PWD}/.ddev" ]]; then
        echo 'ddev exec --'
        return
    fi

    if docker_enabled; then
        declare -r compose_file="$(docker_find_compose_file)"

        if [[ -f "${compose_file}" ]]; then
            declare -ra services=(
                $(sed -nE -e 's/^[[:space:]]+(php|app+):.*$/\1/p' <"${compose_file}" 2>/dev/null)
            )

            if [[ -n "${services[1]}" ]]; then
                echo "$(docker_compose_command) exec -- ${services[1]}"
                return
            fi
        fi
    fi

    if symfony_cli_enabled && (( $+commands[symfony] )) then
        echo 'symfony'
        return
    fi

    if local_enabled; then
        echo 'local'
        return
    fi

    return 1
}

runner_is() {
    [[ "$1" == "$(runner_name)" ]]
}

runner_name() {
    echo "${app_runner%% *}"
}

runner_not_supported() {
    fail "Not supported by current runner: ${app_runner}"
}

config_load_env_files() {
    [[ -f "${PWD}/.env" ]] && config_process <"${PWD}/.env"
    [[ -f "${PWD}/.env.local" ]] && config_process <"${PWD}/.env.local"
}

config_process() {
    grep -E '^[[:space:]]*SF_\w+=' | while IFS='=' read -r key value; do
        export "${key}"="$(unquote <<<"${value}")"
    done
}

feature_enabled() {
    [[ ! "${1}" =~ '^(0|no|off|false|disabled?)$' ]]
}

find_first_existing_file() {
    for file in "$@"; do
        if [[ -f "$file" ]]; then
            echo "${file}" && return
        fi
    done

    return 1
}

fail() {
    error "$@"
    exit 1
}

error() {
    echo "$@" >&2
}

unquote() {
    sed -Ee "s/^\"|'//" -e "s/\"|'$//"
}

main "$@"
