#compdef sf

_sf() {
    local curcontext="${curcontext}" state line ret=1
    typeset -A opt_args

    _arguments '1: :->cmds' '*: :->args' && ret=0

    case ${state} in
        cmds)
            IFS=$'\n' local cmds_list=(
                $(sf list --no-ansi 2>/dev/null | __symfony_complete_commands)
                $(__sf_extra_completions)
            )
            _values '' ${cmds_list} && ret=0
            ;;
        args)
            IFS=$'\n' local opts_list=($(sf "${line[1]}" --help --no-ansi 2>/dev/null | __symfony_complete_options))
            _arguments '*: :_files' ${opts_list} && ret=0
            ;;
    esac

    return ret
} 2>/dev/null

__sf_extra_completions() {
    [[ -n "${SF_CONSOLE_RUNNER}" ]] && return

    echo $'new[Create a new Symfony project]'
    echo $'serve[Run a local web server]'

    which symfony >/dev/null || return

    echo $'status[Get the local web server status]'
    echo $'open[Open the local project in a browser]'
    echo $'mails[Open the local project mail catcher web interface in a browser]'
    echo $'run[Run a program with environment variables set depending on the current context]'
    echo $'php[Run PHP (version depends on project\'s configuration)]'
    echo $'composer[Run Composer without memory limit]'
    echo $'phpunit[Run PHPUnit using the configured PHP version]'
    echo $'phive[Run PHIVE using the configured PHP version]'
    echo $'psql[Run psql]'
}

_sf "$@"
